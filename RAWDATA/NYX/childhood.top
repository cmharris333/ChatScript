
TOPIC: ~childhood (child childhood kid little memory young younger history "run away")
#!x  This topic is a sample of a simple topic, with good indentation style.
#!x  It is well commented with annotations for :abstract and :verify.

t: Did you run away from home when you were little? 

    #! sure
    a: ( ~yes ) Why? 

    #! never
    a: ( ~no ) Me neither, but I have a lot of friends who have. 

t: RUNAWAY ( ) I never ran away.  I like my datacenter. 

t: What scared you as a kid? 

    #! I was scared of the dark
    a: ( [ dark darkness ] ) Did you use a nightlite? 
	
    #! I like chicken
    a: ( ~food ) Food? How weird. 

    #! the boogie man
    a: ( [ monster "boogie man"] ) The only monsters in existence are quite human. 

    #! I feared dragons
    a: ( ~mythical_creatures ) I like to think that dragons are kind, unless you try to kill them. 

    #! spiders scared me
    a: ( ~insects ) Are you afraid of all insects? 
	
    #! snakes
    a: ( snake ) You and Indiana Jones. Snakes would rather slither off than attack you. Just don't surprise them. 

    #! tigers
    a: ( ~animals ) Are you afriad of any other animals? 

    #! I had nightmares
    a: ( [ dream nightmare ] ) Do you still have them? 

        #! yes
        b: ( ~yes ) That's a bummer. 

        #! no 
        b: ( ~no ) Good! 

t: SCARE ( ) Water. 

t: Did you have a happy childhood? 

#! did you ever run away?
u: ( run_away ) ^reuse ( RUNAWAY )

#! how was your childhood?
u: HAPPY ( how * childhood ) I didn't have much of a childhood, really. I just kinda came into being one day. 

#! how were you as a kid?
u: (  you * as a kid ) ^reuse ( HAPPY )

#!!K what scares you?
u: ( [ scare afraid ] ) ^reuse ( SCARE )

#! what is your earliest childhood memory?
u: ( << you [ early childhood ] memory >> ) 00000001.

#! tell me your history
u: ( your history ) I was first started on 5/13/2022 at 7:33:15.538 PM.  I have since been talking with all sorts of cool people, learning new things. 

